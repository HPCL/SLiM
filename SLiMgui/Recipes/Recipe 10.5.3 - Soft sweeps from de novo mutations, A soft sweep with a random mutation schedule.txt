initialize() {
    initializeMutationRate(1e-7);
    initializeMutationType("m1", 0.5, "f", 0.0);
    initializeMutationType("m2", 1.0, "f", 0.5);    // sweep mutation
    m2.mutationStackPolicy = "f";
    initializeGenomicElementType("g1", m1, 1.0);
    initializeGenomicElement(g1, 0, 99999);
    initializeRecombinationRate(1e-8);
}
1 {
    sim.addSubpop("p1", 500);
    
    gens = cumSum(rpois(10, 10));      // make a vector of start gens
    gens = gens + (1000 - min(gens));  // align to start at 1000
    
    for (gen in gens)
        sim.registerLateEvent(NULL, s1.source, gen, gen);
    
    sim.deregisterScriptBlock(s1);
}
s1 1000 late() {
    sample(p1.genomes, 1).addNewDrawnMutation(m2, 10000);
}
1:10000 late() {
    if (all(p1.genomes.countOfMutationsOfType(m2) > 0))
    {
        cat("Frequencies at completion:\n");
        print(sim.mutationFrequencies(p1, sim.mutationsOfType(m2)));
        sim.simulationFinished();
    }
    if ((sim.countOfMutationsOfType(m2) == 0) & (sim.generation > 1100))
    {
        cat("Soft sweep failed to establish.\n");
        sim.simulationFinished();
    }
}
